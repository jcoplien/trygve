plugins {
    id 'antlr'
    id 'application'
}

repositories {
    mavenCentral()
}

dependencies {
    antlr "org.antlr:antlr4:4.10"
}

application {
    mainClass = "info.fulloo.trygve.editor.Main"

    // enable assertions by default
    applicationDefaultJvmArgs = ["-ea"]
}

// make it executable
jar {
    manifest {
        attributes(
            'Main-Class': "info.fulloo.trygve.editor.Main"
        )
    }
}

// extra files that need to be compiled
sourceSets {
    main {
        java {
            srcDir 'build/generated-src/antlr/main'
        }
    }
}

// default arguments for "gradlew run"
run {
    args "-gui"
}

// include files into the final distribution
distZip {
    into(project.name) {
        from '.'
        include 'examples/*'
        include 'tests/*'
        include 'LICENSE'
    }
}

distTar {
    into(project.name) {
        from '.'
        include 'examples/*'
        include 'tests/*'
        include 'LICENSE'
    }
}
