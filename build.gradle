apply plugin: 'java'
apply plugin: 'application'

apply plugin: 'idea'
apply plugin: 'eclipse'

repositories {
    jcenter()
}

mainClassName = "info.fulloo.trygve.editor.Main"

jar {
  manifest {
    attributes 'Main-Class': 'info.fulloo.trygve.editor.Main'
  }
}

applicationDefaultJvmArgs = ["-ea"]
compileJava {
    options.fork = true
    options.incremental = true
}

sourceSets.main.java.srcDirs  = [
    'src/main/java',
    'src/main/generated'
]

distZip {
    into(project.name) {
        from '.'
        include 'examples/*'
        include 'tests/*'
    }
}

distTar {
    into(project.name) {
        from '.'
        include 'examples/*'
        include 'tests/*'
    }
}

dependencies {
    compile files('lib/antlr-runtime-4.5.1.jar')
}

task antlr(type: Exec) {
    workingDir 'src/main/java'

    if (System.getProperty('os.name').toLowerCase().contains('windows') ||
        System.getProperty('os.name').toLowerCase().contains('Windows') ) {
        commandLine 'java',
            '-Dline.separator=\n',
            '-jar', "$rootProject.projectDir\\lib\\antlr-4.5.1-complete.jar",
            '-o', "$rootProject.projectDir\\src\\main\\generated",
            'info\\fulloo\\trygve\\parser\\Kant.g4'
    } else {
        commandLine 'java',
            '-jar', "$rootProject.projectDir/lib/antlr-4.5.1-complete.jar",
            '-o', "$rootProject.projectDir/src/main/generated",
            'info/fulloo/trygve/parser/Kant.g4'
    }
}