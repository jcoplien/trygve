plugins {
    id 'antlr'
    id 'application'
}

repositories {
    mavenCentral()
}

dependencies {
    antlr "org.antlr:antlr4:4.10"
    implementation "org.antlr:antlr4-runtime:4.10"
}

// exclude antlr, we only need its runtime in the dist.
configurations.implementation {
    exclude group: "org.antlr", module: "antlr4"
}

generateGrammarSource {
    outputDirectory = new File("${project.projectDir}/src/main/java/info/fulloo/trygve/parser")
}

application {
    mainClass = "info.fulloo.trygve.editor.Main"

    // enable assertions by default
    applicationDefaultJvmArgs = ["-ea"]

}

build {
    configure {
        dependsOn generateGrammarSource
    }
    // automatically install the distribution in build/install/trygve
    finalizedBy "installDist"
}

// make it executable
jar {
    manifest {
        attributes(
            'Main-Class': "info.fulloo.trygve.editor.Main"
        )
    }

    /*
    // Make a fat jar
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    */
}

// default arguments for "gradlew run"
run {
    args "-gui"
}

// include files into the final distribution
distZip {
    into(project.name) {
        from '.'
        include 'examples/*'
        include 'tests/*'
        include 'LICENSE'
    }
}

distTar.enabled = false
